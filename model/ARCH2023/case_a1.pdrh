// ARCH 2023 Minimal example A, variant 1

// Assumes dReal in the path

// This experiment is performed on a Linux VM
// ProbReach version: 1.4 (68e9d301d88273916d7277ac592636483a26721b)
// dReal version: v3.16.05 (commit 3cc1370a5789)
// OS: Ubuntu 22.04.2 LTS
// RAM: 8GB
// CPU info: 
// 	Manufacturer: Intel Core Processor (Skylake)
//	Cache size: 16384 KB
//	Frequency (MHz): 2591.785
//
// How to run: 
//	time ./formal_verifier -t 4 -k 1 -e 0.005 --partition-prob case_a1.pdrh
//
// Output: 
//	[2.8632455143548752e-01,2.9104623812769659e-01] | 4.7216866922090706e-03
//
//	real	1m7.854s
//	user	3m27.870s
//	sys	0m42.273s

[-1,100] x; 
[0,20] tau; // time

dist_pdf(0.1 * exp((-0.1)*x_1), 0, inf, 0) x_1;
dist_pdf(0.08 * exp((-0.08)*x_2), 0, inf, 0) x_2;

{
    mode 0;

    time: [0, 10];

    flow:
        d/dt[tau] = 1;
        d/dt[x]   = 2;
    jump:
    (and (tau = x_2) (tau < x_1) (tau < 5.8)) ==> @1 (and (x' = x) (tau' = tau));
}


{
    mode 1;         // final mode
    time: [0, 10];
    flow:
        d/dt[tau] = 1;
        d/dt[x]   = -3;
    jump:
}

init:
@0 (and (x = 0) (tau = 0));

goal:
@1 (and (tau <= 10) (x <= -1));
