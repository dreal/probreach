// ARCH 2023 Minimal example A, variant 1

// Assumes dReal in the path

// This experiment is performed on a Linux VM
// ProbReach version: 1.4 (68e9d301d88273916d7277ac592636483a26721b)
// dReal version: v3.16.05 (commit 3cc1370a5789)
// OS: Ubuntu 22.04.2 LTS
// RAM: 8GB
// CPU info: 
// 	Manufacturer: Intel Core Processor (Skylake)
//	Cache size: 16384 KB
//	Frequency (MHz): 2591.785
//
// How to run the formal approach: 
//	time ./formal_verifier -t 6 -k 1 -e 0.005 --partition-prob case_a1.pdrh
//
// Output:
//  [2.8632455143548746e-01,2.9104623812769648e-01] | 4.7216866922090150e-03
//
//  real	0m45.052s
//  user	3m15.569s
//  sys	0m36.197s
//
//
// How to run the statistical approach (Bayesian estimations algorithm, default confidence value = 0.99):
//  time ./mc_verifier -t 6 -k 1 -e 0.005 case_a1.pdrh
//
// Output:
//  [2.8211340206185564e-01,2.9211340206185565e-01] | 1.0000000000000009e-02
//
//  real	3m31.355s
//  user	17m35.640s
//  sys	3m26.861s


[-1,100] x; 
[0,20] tau; // time

dist_exp(0.1) x_1;
dist_exp(0.08) x_2;

{
    mode 0;

    time: [0, 10];

    flow:
        d/dt[tau] = 1;
        d/dt[x]   = 2;
    jump:
    (and (tau = x_2) (tau < x_1) (tau < 5.8)) ==> @1 (and (x' = x) (tau' = tau));
}


{
    mode 1;         // final mode
    time: [0, 10];
    flow:
        d/dt[tau] = 1;
        d/dt[x]   = -3;
    jump:
}

init:
@0 (and (x = 0) (tau = 0));

goal:
@1 (and (tau <= 10) (x <= -1));
