
// ARCH 2023 Minimal example A, variant 2

//time ./formal_verifier -k 1 -e 0.005 --partition-prob  case_a2.pdrh 
//[4.4651246572725334e-01,4.5026208720019428e-01] | 3.7496214729409449e-03

//real	2m56.899s
//user	4m0.474s
//sys	2m53.068s


[-1,100] x; 
[0,100] x_2; 
[0,20] tau; // time

dist_pdf(0.1 * exp((-0.1)*x_1), 0, inf, 1/0.1) x_1;
dist_normal(5, 2) y;

{
    mode 0;
    time: [0, 10];
    flow:
        d/dt[tau] = 1;
        d/dt[x]   = 2;
        d/dt[x_2] = 0;
    jump:
    (and (tau = x_2) (tau < x_1) (tau < 5.8)) ==> @1 (and (x' = x) (tau' = tau));
}

{
    mode 1;         // final mode
    time: [0, 10];
    flow:
        d/dt[tau] = 1;
        d/dt[x]   = -3;
        d/dt[x_2] = 0;
    jump:
}

init:
@0 (and (x = 0) (tau = 0) (x_2 = sqrt(y*y)));

goal:
@1 (and (tau <= 10) (x <= -1));
