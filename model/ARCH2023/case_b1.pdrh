// ARCH 2023 Minimal example B, variant 1


#define TMAX 12

//Assumes dreal is in the path!

//For TMAX = 10, four-core CPU
//time ./formal_verifier -k 1 -e 0.001 --partition-prob case_b1.pdrh 
//[2.6624287163869087e-01,2.6686636016518983e-01] | 6.2348852649896092e-04

//real	0m28.872s
//user	1m16.157s
//sys	0m20.486s


//For TMAX = 12, four-core CPU
//time ./formal_verifier -k 1 -e 0.001 --partition-prob case_b1.pdrh 
//[2.8762080758570635e-01,2.8858315849698063e-01] | 9.6235091127427896e-04

//real	3m23.205s
//user	9m18.283s
//sys	2m26.923s


[-1,100] x; 
[0,20] tau; // time

dist_pdf(0.1 * exp((-0.1)*x_1), 0, inf, 0) x_1;
dist_pdf(0.08 * exp((-0.08)*x_2), 0, inf, 0) x_2;

{
    mode 0;
    time: [0, 10];
    flow:
        d/dt[tau] = 1;
        d/dt[x]   = 2;
    jump:
    // instead of introducing a mode and variable y, just add 2 to tau
    (and (tau = x_2) (tau < x_1) (tau < 5.8)) ==> @2 (and (x' = x) (tau' = tau + 2));
    (and (tau = x_2) (tau < x_1) (tau < 5.8)) ==> @3 (and (x' = x) (tau' = tau + 2));
}


{
    mode 2;         // goal mode
    time: [0, 10];
    flow:
        d/dt[tau] = 1;
        d/dt[x]   = -3;
    jump:
}

{
    mode 3;
    time: [0, 10];
    flow:
        d/dt[tau] = 1;
        d/dt[x]   = 0;
    jump:
}


init:
@0 (and (x = 0) (tau = 0));

goal:
@2 (and (tau <= TMAX) (x <= -1));
